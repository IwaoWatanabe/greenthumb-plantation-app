
buildscript {
println "gradle version: " + gradle.gradleVersion
println "JVM: " + System.getProperty("java.home") + " arch: " + System.getProperty("os.arch")
// Display information about the JVM used for the build

repositories {
  mavenLocal()
  repositories {
      maven { url "https://plugins.gradle.org/m2/" }
  }
  maven {
    url 'https://pkgs.dev.azure.com/iwaowatanabe/kt-learning/_packaging/OldVM/maven/v1'
    // see: https://dev.azure.com/iwaowatanabe/kt-learning/_artifacts/feed/OldVM
  }
  mavenCentral()
}
dependencies {
  classpath 'org.gretty:gretty:2.3.1'
  if (java.lang.Integer.getInteger("java.specification.version") < 1.8) {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  }
  else if (gradle.gradleVersion < '7.0') {
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
  }
  else {
    classpath 'com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:7.1.2'
  }
}}

plugins {
  // id 'com.github.johnrengelman.shadow' version '5.2.4'
  // id("org.gretty") version "2.3.1"
  id 'maven-publish'
}

def sourceEncoding = 'utf-8'
def documentEncoding = 'utf-8'
def env = System.env
def gver = gradle.gradleVersion
def sp = System.properties
def osname = sp['os.name'].toLowerCase()
def osarch = sp['os.arch']
def userHome = sp['user.home']
def gverinfo = "Gradle ${gradle.gradleVersion} (${sp['os.name']}:${sp['os.arch']}:${sp['os.version']})"

allprojects {
apply plugin: 'java'
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['test']
if (gver < '5.0') {
  sourceSets.main.output.classesDir = 'build/classes'
  sourceSets.main.output.resourcesDir = 'build/resources'
  sourceSets.test.output.classesDir = 'build/test-classes'
  sourceSets.test.output.resourcesDir = 'build/test-resources'
}
repositories {
  mavenLocal()
  maven {
    url 'https://pkgs.dev.azure.com/iwaowatanabe/kt-learning/_packaging/OldVM/maven/v1'
    // see: https://dev.azure.com/iwaowatanabe/kt-learning/_artifacts/feed/OldVM
  }
  maven { url 'https://repository.jboss.org/maven2'  }
  mavenCentral()
}
tasks.withType(JavaCompile) {
  // options.compilerArgs << '-Xlint:unchecked,divzero'
  options.deprecation = false
  options.encoding = sourceEncoding
  doFirst {
    def bootClassPath = ''
    if (sourceCompatibility == '1.8' && env.JRE8_HOME != null)
        bootClassPath ="${env.JRE8_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.7' && env.JRE7_HOME != null)
        bootClassPath = "${env.JRE7_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.6' && env.JRE6_HOME != null)
        bootClassPath = "${env.JRE6_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.5' && env.JRE5_HOME != null)
        bootClassPath = "${env.JRE5_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.4' && env.JRE4_HOME != null)
        bootClassPath = "${env.JRE4_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.3' && env.JRE3_HOME != null)
        bootClassPath = "${env.JRE3_HOME}/lib/rt.jar"

    if (bootClassPath != '') {
        println "INFO: bootstrapClasspath: ${bootClassPath}"
        options.fork = true
        if (gver < '5.0')
          options.bootClasspath = "${bootClassPath}"
      // ↑ for gradle 4.x with JDK7
        else
          options.bootstrapClasspath = files("${bootClassPath}")
      // ↑ for gradle 5.x with JDK8 or later
    }
  }
}
javadoc {
  source = sourceSets.main.allJava
  classpath = sourceSets.main.compileClasspath
  options.charSet = documentEncoding
  options.encoding = sourceEncoding
  if (env.DEBUG != null) options.memberLevel = JavadocMemberLevel.PACKAGE
  options.addStringOption('Xdoclint:none', '-quiet')
  doLast {
    println "INFO: open ${project.docsDir}/javadoc/index.html"
  }
}

task zipdoc(type: Zip, dependsOn: javadoc) {
  description 'javadoc を zipでアーカイブする'
  if (gver < '7.0') {
    destinationDir = file('../apidocs')
    classifier = "apidoc-" + new Date().format('yyyy-MMdd')
    extension = 'zip'
  }
  else {
    destinationDirectory = file('../apidocs')
    archiveClassifier = "apidoc-" + new Date().format('yyyy-MMdd')
    archiveExtension = 'zip'
  }
  from "${project.docsDir}/javadoc"
  excludes = ['build', '.*']
  doLast {
    println "INFO: created: ${archivePath}"
  }
}

task copylib(type: Copy) {
  description 'Collect dependent libraries and store them in the lib folder'
  if (gver <'7.0') {
    // from (configurations.compile + configurations.testCompile)
    // from configurations.compile
    from configurations.runtimeClasspath
  }
  else {
    from configurations.runtimeClasspath
  }
  into "lib"
}}

subprojects {
// sourceCompatibility = 1.6

task localDeploy {
dependsOn "jar"
def LIBS_DEPLOY_PATH="${rootDir}/libs"
doFirst {
  println "from :build/libs/${project.jar.archiveName} To: ${LIBS_DEPLOY_PATH}"
}
doLast {
  copy {
    from "build/libs/${project.jar.archiveName}"
    into "${LIBS_DEPLOY_PATH}"
  }
}}

task hotDeploy {
dependsOn "ear"
doFirst {
  println "from :build/libs/${project.ear.archiveName} To: ${env.DEPLOY_PATH}"
}
doLast {
  copy {
    from "build/libs/${project.ear.archiveName}"
    into "${env.DEPLOY_PATH}"
  }
}}}


project(':hello') {
apply plugin: 'application'
description 'first project'
mainClassName = 'args'
// sourceCompatibility = '1.3'
// targetCompatibility = '1.1'
dependencies {
  testImplementation 'junit:junit:3.+'
}
jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}
task hello(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'hello'
  args "aa bb cc".split('\\s+')
}}

project(':GreenthumbNursery') {
apply plugin: 'application'
description 'A comprehensive desktop application for nursery management built with Java Swing and MySQL'
mainClassName = 'com.greenthumb.GreenthumbNurseryApp'
sourceCompatibility = '10'
// targetCompatibility = '1.1'
dependencies {
  implementation 'ch.qos.logback:logback-classic:1.4.7'
  runtimeOnly 'org.slf4j:slf4j-api:2.0.7'
  runtimeOnly 'mysql:mysql-connector-java:8.0.33'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
  testImplementation 'org.junit.platform:junit-platform:1.9.2'
}
jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}}


